! Module file: fom_to_x
!
! CNS module
! **********
!
! Authors: Axel Brunger and Paul Adams
!
! copyright Yale University
!
! Function:
!    Calculates X from FOM for both centric and acentric reflections
!
! Requirements:
!    Needs to be called within xray

module {fom_to_x}

(
  &fom=fom;              {string: array containing FOM}
  &x=x;                  {string: output array containing X}
  &selection=(all);      {selection: reflections to use}
)

set message ? end
evaluate ($message_old=$result)
set echo ? end
evaluate ($echo_old=$result)
if ( $log_level = verbose ) then
  set echo=on message=normal end
else
  set echo=off message=off end
end if

checkversion 1.3

evaluate ($fom_max=0.99999999)

do (&x=(log((1+min($fom_max,&fom))/(1-min($fom_max,&fom))))/2)
                          (&selection and centric)

do (&x=i1overi0inv(&fom)) (&selection and acentric)

do (&x=&x/2) (&selection)

set message=$message_old echo=$echo_old end
