{+ file: surface_plot.inp +}
{+ directory: general +}
{+ description: Lee & Richards surface accessibility calculation +}
{+ authors: Axel T. Brunger, and Paul D. Adams +}
{+ copyright: Yale University +}

{+ reference: B. Lee, and F.M. Richards, The Interpretation of 
              Protein Structures:  Estimation of Static Accessibility, 
              J. Mol. Biol. 55, 379-400, 1971. +}

{- Guidelines for using this file:
   - all strings must be quoted by double-quotes
   - logical variables (true/false) are not quoted
   - do not remove any evaluate statements from the file -}

{- begin block parameter definition -} define(

{============================ coordinates ============================}

{* coordinate file *}
{===>} coordinate_infile="amy.pdb";

{==================== molecular information ==========================}

{* topology files *}
{===>} topology_infile_1="CNS_TOPPAR:protein.top";
{===>} topology_infile_2="CNS_TOPPAR:dna-rna.top";
{===>} topology_infile_3="CNS_TOPPAR:water.top";
{===>} topology_infile_4="CNS_TOPPAR:ion.top";
{===>} topology_infile_5="CNS_TOPPAR:carbohydrate.top";
{===>} topology_infile_6="";
{===>} topology_infile_7="";
{===>} topology_infile_8="";

{* linkage files for linear, continuous polymers (protein, DNA, RNA) *}
{===>} link_infile_1="CNS_TOPPAR:protein.link";
{===>} link_infile_2="CNS_TOPPAR:dna-rna-pho.link";
{===>} link_infile_3="";

{* parameter files *}
{===>} parameter_infile_1="CNS_TOPPAR:protein_rep.param";
{===>} parameter_infile_2="CNS_TOPPAR:dna-rna_rep.param";
{===>} parameter_infile_3="CNS_TOPPAR:water_rep.param";
{===>} parameter_infile_4="CNS_TOPPAR:ion.param";
{===>} parameter_infile_5="CNS_TOPPAR:carbohydrate.param";
{===>} parameter_infile_6="";
{===>} parameter_infile_7="";
{===>} parameter_infile_8="";

{* molecular topology file: optional (leave blank for auto generation) *}
{* 
   Auto generation of the molecular topology from the coordinates should only 
   be used if:
   (1) Each distinct protein, DNA, or RNA chain must have a separate segid 
       (or chainid if the chainid is non-blank). 
   (2) Each contiguous protein, RNA, or RNA chain must not be disrupted by 
       other types of residues or ligands.  Rather, these other residues 
       should be listed after protein, RNA/DNA chains. 
   (3) Disulphides are automatically detected based on distances between the sulfur atoms
      (must be less than 3 A apart).
   (4) Broken protein/RNA/DNA chains without terminii must be more than 2.5 A apart to be recognized as such.
   (5) N-linked glycan links are automatically recognized if the bonded atoms are less than 2.5 A apart.
   (6) Automatic generation cannot be used with alternate conformations. 
   For ligands, the user must make suitable topology and parameter files.
   For non-standard covalent linkages, the custom patch file should be used.
   Alternatively, the generate.inp or generate_easy.inp task files
   can be used to generated the mtf prior to running this task file.
    *}
{===>} structure_infile="amy.mtf";

{* for auto generation: extra linkages and modifications by custom patches *}
{===>} patch_infile="";

{========================== parameters ==============================}

{* probe radius in Angstroms *}
{===>} probe=1.4;

{========================== atom selection ===========================}

{* select atoms to be included in calculation *}
{===>} atom_select=(known and not(hydrogen));

{* select main chain atoms *}
{* note: atoms outside this selection will be considered to be
         side chain atoms *}
{===>} atom_main=(name ca or name n or name c or name o or name ot+);

{=========================== output files ============================}

{* list average or total surface area per residue *}
{+ choice: "total" "average" +}
{===>} list_mode="average";

{* output list file *}
{===>} list_outfile="surface_plot.list";

{* output coordinate file *}
{* the B-factor column will contain the per atom surface accessibility *}
{===>} coordinate_outfile="surface_plot.pdb";

{===========================================================================}
{        things below this line do not normally need to be changed          }
{===========================================================================}

 ) {- end block parameter definition -}

 checkversion 1.3

 evaluate ($log_level=quiet)

 if ( $log_level = verbose ) then
   set message=normal echo=on end
 else
   set message=off echo=off end
 end if

 if ( &BLANK%structure_infile = true ) then
 
    {- read topology files -}
    topology
     evaluate ($counter=1)
     evaluate ($done=false)
     while ( $done = false ) loop read
      if ( &exist_topology_infile_$counter = true ) then
         if ( &BLANK%topology_infile_$counter = false ) then
            @@&topology_infile_$counter
         end if
      else
        evaluate ($done=true)
      end if
      evaluate ($counter=$counter+1)
     end loop read
    end
    
    @CNS_XTALMODULE:mtfautogenerate (
                                  coordinate_infile=&coordinate_infile;
                                  convert=true;
                                  separate=true;
                                  atom_delete=(not known);
                                  hydrogen_flag=true;
                                  break_cutoff=2.5;
                                  disulphide_dist=3.0;
                                  carbo_dist=2.5;
                                  patch_infile=&patch_infile;
                                  O5_becomes="O";
                                 )

 else

   structure @&structure_infile end
   coordinates @&coordinate_infile

 end if

 {- read parameter files -}
 parameter
  evaluate ($counter=1)
  evaluate ($done=false)
  while ( $done = false ) loop read
   if ( &exist_parameter_infile_$counter = true ) then
      if ( &BLANK%parameter_infile_$counter = false ) then
         @@&parameter_infile_$counter
      end if
   else
     evaluate ($done=true)
   end if
   evaluate ($counter=$counter+1)
  end loop read
 end

 set message=normal echo=on end
 
 set display=&list_outfile end

 surface
    mode=access
    rh2o=&probe
    selection=&atom_select
    radius=vdw
 end
 
 display >>>> Surface plot for:
 display >>>>   coordinate set= &STRIP%coordinate_infile
 display
 if ( &list_mode = "average" ) then
   display                                   average accessible area
 elseif ( &list_mode = "total" ) then
   display                                    total accessible area
 end if
 display  segid  resid  resname     residue      mainchain       sidechain
 display

 set display=OUTPUT end

 for $id in id ( &atom_select and tag ) loop bfac

   show (segid) (id $id)
   evaluate ($segid=$result)
   show (resid) (id $id)
   evaluate ($resid=$result)
   show (resname) (id $id)
   evaluate ($resname=$result)

   if ( &list_mode = "average" ) then

     show ave(rmsd) (segid $segid and resid $resid)
     evaluate ($access=$result)

     show ave(rmsd) (segid $segid and resid $resid and &atom_main)
     evaluate ($access_main=$result)

     show ave(rmsd) (segid $segid and resid $resid and not(&atom_main))
     evaluate ($access_side=$result)

   elseif ( &list_mode = "total" ) then

     show sum(rmsd) (segid $segid and resid $resid)
     evaluate ($access=$result)

     show sum(rmsd) (segid $segid and resid $resid and &atom_main)
     evaluate ($access_main=$result)

     show sum(rmsd) (segid $segid and resid $resid and not(&atom_main))
     evaluate ($access_side=$result)

   else

     display unknown list mode
     abort

   end if

   set display=&list_outfile end
   display  $segid[a4]    $resid[a4]    $resname[a4]   $access[f10.4] \
   $access_main[f10.4]      $access_side[f10.4]
   set display=OUTPUT end
   
 end loop bfac

 do (b=rmsd) ( all ) 

 set remarks=reset end
 set remarks=accumulate end

 remarks Lee & Richards surface accessibility calculation
 remarks Surface area for each atom is in the B-factor column

 write coordinates output=&coordinate_outfile format=PDBO end
 
 stop
